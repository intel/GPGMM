# Copyright 2019 The Dawn Authors
# Copyright 2021 The GPGMM Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("../../../build_overrides/gpgmm_overrides_with_defaults.gni")

import("//build_overrides/build.gni")
import("${gpgmm_root_dir}/build_overrides/gpgmm_features.gni")

# Use Chromium's dcheck_always_on when available so that we respect it when
# running tests on the GPU builders
if (build_with_chromium) {
  import("//build/config/dcheck_always_on.gni")
} else {
  dcheck_always_on = false
}

if (build_with_chromium) {
  import("//build/config/sanitizers/sanitizers.gni")
} else {
  use_fuzzing_engine = false
}

###############################################################################
# Common GPGMM configs
###############################################################################

config("gpgmm_public_config") {
  include_dirs = [
    "${gpgmm_root_dir}/src",
    "${gpgmm_root_dir}/src/include",
  ]
}

config("gpgmm_common_config") {
  defines = []
  if (gpgmm_always_assert || dcheck_always_on || is_debug ||
      use_fuzzing_engine) {
    defines += [ "GPGMM_ENABLE_ASSERTS" ]
  }

  if (use_fuzzing_engine) {
    # Does a hard abort when an assertion fails so that fuzzers catch and parse the failure.
    defines += [ "GPGMM_ABORT_ON_ASSERT" ]
  }

  defines += [ "GPGMM_IMPLEMENTATION" ]

  if (gpgmm_shared_library) {
    defines += [ "GPGMM_SHARED_LIBRARY" ]
  }

  if (!is_debug && !gpgmm_force_tracing) {
    defines += [ "GPGMM_DISABLE_TRACING" ]
  }

  if (gpgmm_enable_allocator_leak_checks) {
    defines += [ "GPGMM_ENABLE_ALLOCATOR_LEAK_CHECKS" ]
  }

  if (gpgmm_enable_assert_on_warning) {
    defines += [ "GPGMM_ENABLE_ASSERT_ON_WARNING" ]
  }

  if (gpgmm_disable_size_cache) {
    defines += [ "GPGMM_DISABLE_SIZE_CACHE" ]
  }

  if (gpgmm_enable_device_leak_checks) {
    defines += [ "GPGMM_ENABLE_DEVICE_LEAK_CHECKS" ]
  }

  if (gpgmm_enable_memory_leak_checks) {
    defines += [ "GPGMM_ENABLE_MEMORY_LEAK_CHECKS" ]
  }

  # Only internal build targets can use this config, this means only targets in
  # this BUILD.gn file and related subdirs.
  visibility = [ "../../*" ]

  cflags = []

  # Enable more warnings then using GPGMM under third_party/gpgmm.
  # Add them only when building in standalone because we control which compiler
  # version we use. Otherwise we risk breaking projects depending on GPGMM when
  # they use a different compiler version.
  if (gpgmm_standalone && is_clang) {
    cflags += [
      "-Wconditional-uninitialized",
      "-Wcstring-format-directive",
      "-Wc++11-narrowing",
      "-Wdeprecated-copy",
      "-Wdeprecated-copy-dtor",
      "-Wduplicate-enum",
      "-Wextra-semi-stmt",
      "-Wimplicit-fallthrough",
      "-Winconsistent-missing-destructor-override",
      "-Winvalid-offsetof",
      "-Wmissing-field-initializers",
      "-Wnon-c-typedef-for-linkage",
      "-Wpessimizing-move",
      "-Wrange-loop-analysis",
      "-Wredundant-move",
      "-Wshadow-field",
      "-Wstrict-prototypes",
      "-Wsuggest-destructor-override",
      "-Wtautological-unsigned-zero-compare",
      "-Wunreachable-code-aggressive",
    ]

    # Allow comparison against type limits that might be tautological on 32bit
    # or 64bit systems. Without this the following produces an error on 64bit:
    #
    #   if (myUint64 > std::numeric_limits<size_t>::max()) {...}
    cflags += [ "-Wno-tautological-type-limit-compare" ]

    if (is_win) {
      cflags += [
        # clang-cl doesn't know -pedantic, pass it explicitly to the clang driver
        "/clang:-pedantic",

        # Allow the use of __uuidof()
        "-Wno-language-extension-token",
      ]
    } else {
      cflags += [ "-pedantic" ]
    }
  }

  if (!is_clang && is_win) {
    # MSVC considers extended enums invalid switch values.
    cflags += [ "/wd4063" ]
    if (gpgmm_is_winuwp) {
      # /ZW makes sure we don't add calls that are forbidden in UWP.
      # and /EHsc is required to be used in combination with it,
      # even if it is already added by the windows GN defaults,
      # we still add it to make every /ZW paired with a /EHsc
      cflags_cc = [
        "/ZW:nostdlib",
        "/EHsc",
      ]
    }

    # MSVC requires a dynamically linked library (.dll) to use a multi-thread
    # dynamic runtime.
    if (gpgmm_shared_library) {
      if (is_debug) {
        cflags += [ "/MDd" ]
      } else {
        cflags += [ "/MD" ]
      }
    }
  }
}

source_set("gpgmm_common_sources") {
  deps = [
    "${gpgmm_root_dir}/src/gpgmm:gpgmm_headers",
    "${gpgmm_root_dir}/src/gpgmm/utils:gpgmm_utils_sources",
  ]

  libs = []
  data_deps = []

  configs += [ "${gpgmm_root_dir}/src/gpgmm/common:gpgmm_common_config" ]

  sources = [
    "AllocatorNode.cpp",
    "AllocatorNode.h",
    "BlockAllocator.h",
    "BuddyBlockAllocator.cpp",
    "BuddyBlockAllocator.h",
    "BuddyMemoryAllocator.cpp",
    "BuddyMemoryAllocator.h",
    "ConditionalMemoryAllocator.cpp",
    "ConditionalMemoryAllocator.h",
    "Defaults.h",
    "Error.h",
    "EventMessage.cpp",
    "EventMessage.h",
    "EventTraceWriter.cpp",
    "EventTraceWriter.h",
    "GPUInfo.h",
    "IndexedMemoryPool.cpp",
    "IndexedMemoryPool.h",
    "JSONSerializer.cpp",
    "JSONSerializer.h",
    "LIFOMemoryPool.cpp",
    "LIFOMemoryPool.h",
    "Memory.cpp",
    "Memory.h",
    "MemoryAllocation.cpp",
    "MemoryAllocation.h",
    "MemoryAllocator.cpp",
    "MemoryAllocator.h",
    "MemoryCache.h",
    "MemoryPool.cpp",
    "MemoryPool.h",
    "PooledMemoryAllocator.cpp",
    "PooledMemoryAllocator.h",
    "SegmentedMemoryAllocator.cpp",
    "SegmentedMemoryAllocator.h",
    "SlabBlockAllocator.cpp",
    "SlabBlockAllocator.h",
    "SlabMemoryAllocator.cpp",
    "SlabMemoryAllocator.h",
    "StandaloneMemoryAllocator.cpp",
    "StandaloneMemoryAllocator.h",
    "TraceEvent.cpp",
    "TraceEvent.h",
    "WorkerThread.cpp",
    "WorkerThread.h",
  ]
}
