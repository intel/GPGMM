# Copyright 2021 The GPGMM Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/gclient_args.gni")

# Enable the compilation for UWP
gpgmm_is_winuwp = is_win && target_os == "winuwp"

# Non-standalone builds should not checkout integrations used for testing.
# Rather then require GN clients to modify DEPS to disable,
# always disable them here by default or when undefined.
if (!defined(checkout_dawn)) {
  checkout_dawn = false
}

if (!defined(checkout_webnn)) {
  checkout_webnn = false
}

declare_args() {
  # Enable GPGMM's ASSERTs even in release builds
  # Sets -dGPGMM_ENABLE_ASSERTS
  gpgmm_always_assert = false

  # Enables event tracing even in release builds.
  # Sets -dGPGMM_FORCE_TRACING
  gpgmm_force_tracing = false

  # Enables the compilation of the DirectX 12 backend.
  gpgmm_enable_d3d12 = is_win

  # Enables the compilation of the Vulkan backend
  gpgmm_enable_vk = is_linux || is_chromeos || (is_win && !gpgmm_is_winuwp) ||
                    is_fuchsia || is_android

  # Enables compilation of Dawn's end2end tests.
  gpgmm_enable_dawn = checkout_dawn

  # Enables compilation of WebNN's end2end tests.
  gpgmm_enable_webnn = checkout_webnn

  # Build GPGMM as a shared library.
  # Sets -dGPGMM_SHARED_LIBRARY
  gpgmm_shared_library = false

  # Enables checking of allocator leaks.
  # Sets -dGPGMM_ENABLE_PRECISE_ALLOCATOR_DEBUG
  gpgmm_enable_allocator_checks = is_debug

  # Enables ASSERT on severity functionality.
  # Sets -dGPGMM_ENABLE_ASSERT_ON_WARNING
  gpgmm_enable_assert_on_warning = false

  # Enables warming of caches with common sizes.
  # Sets -dGPGMM_ENABLE_SIZE_CACHE
  gpgmm_enable_size_cache = true

  # Enables checking of device leaks.
  # Sets -dGPGMM_ENABLE_DEVICE_CHECKS
  gpgmm_enable_device_checks = false
}

# Declare separately since both depend on |gpgmm_enable_vk| and GN does not allow
# args to depend on another args in the same declare_args().
declare_args() {
  # Uses our built version of the Vulkan validation layers
  gpgmm_enable_vk_validation_layers =
      gpgmm_enable_vk && ((is_linux && !is_chromeos) || is_win || is_mac)

  # Uses our built version of the Vulkan loader on platforms where we can't
  # assume to have one present at the system level.
  gpgmm_enable_vk_loader =
      gpgmm_enable_vk && (is_mac || (is_linux && !is_android))

  # Configures Vulkan functions by statically importing them (ie. importing the Vulkan loader symbols).
  gpgmm_vk_static_functions = true

  # Configures Vulkan functions by dynamically importing them (ie. using vkGetInstanceProcAddr and vkGetDeviceProcAddr).
  gpgmm_vk_dynamic_functions = false
}
